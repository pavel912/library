plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.liquibase.gradle' version '2.0.4'
}

group 'pavel.library'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainClass = 'pavel.library.AppApplication'
}

dependencies {

    implementation(
            'org.springframework.boot:spring-boot-starter',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-validation',
            'org.springframework.security:spring-security-core',
            'org.springframework.security:spring-security-config',
            'org.springframework.security:spring-security-web',
            'org.springframework.data:spring-data-commons',
            'org.springdoc:springdoc-openapi-ui:1.5.12',
            'org.liquibase:liquibase-core:4.6.1',
    )

    liquibaseRuntime(
            'org.liquibase:liquibase-core:4.6.1',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.liquibase.ext:liquibase-hibernate5:4.5.0',
            "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2",
            sourceSets.main.output
    )

    annotationProcessor(
            'org.projectlombok:lombok',
            'javax.annotation:javax.annotation-api:1.3.2',
            'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final',
            'com.querydsl:querydsl-apt:5.0.0:jpa'
    )

    runtimeOnly(
            'com.h2database:h2:1.4.200',
            'org.postgresql:postgresql:42.5.4'
    )

    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            'org.projectlombok:lombok:1.18.22'
    )

    compileOnly 'org.projectlombok:lombok'

}

test {
    useJUnitPlatform()
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn test
}

diffChangeLog {
    dependsOn compileJava
}


liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
            url 'jdbc:h2:./libraryDb'
            username 'admin'
            password 'admin'
            referenceUrl 'hibernate:spring:hexlet.code.domain' +
                    '?dialect=org.hibernate.dialect.H2Dialect' +
                    '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
        }
    }
}